#include <Wire.h>
#include <Tiny4kOLED.h>

const byte LED_RUN   = 1;
const byte BTN_START = 4;
const byte BTN_RESET = 3;

volatile uint32_t msElapsed = 0;
bool running = false;

void setup() {
  pinMode(LED_RUN, OUTPUT);
  pinMode(BTN_START, INPUT_PULLUP);
  pinMode(BTN_RESET, INPUT_PULLUP);

  oled.begin();
  oled.clear();
  oled.setFont(FONT8X16P);
  oled.setFontX2();
  oled.on();

  oled.setCursor(40, 0);
  oled.print(F("00"));
  oled.setCursor(40, 4);
  oled.print(F("00"));
}

void loop() {
  static bool prevS = HIGH, prevR = HIGH;
  bool s = digitalRead(BTN_START);
  bool r = digitalRead(BTN_RESET);

  if (prevS && !s) running = !running;
  prevS = s;

  if (prevR && !r && !running) msElapsed = 0;
  prevR = r;

  static uint32_t tPrev = 0;
  uint32_t now = millis();
  if (running) msElapsed += now - tPrev;
  tPrev = now;

  static uint16_t prevSecShown = 0xFFFF;
  uint32_t totalSec = msElapsed / 1000;
  if (totalSec != prevSecShown) {
    prevSecShown = totalSec;

    uint8_t sec = totalSec % 60;
    uint8_t min = (totalSec / 60) % 100;

    char buf[3];
    snprintf(buf, sizeof(buf), "%02u", min);
    oled.setCursor(40, 0);
    oled.print(buf);

    snprintf(buf, sizeof(buf), "%02u", sec);
    oled.setCursor(40, 4);
    oled.print(buf);
  }

  static uint32_t tBlink = 0;
  if (running && now - tBlink >= 250) {
    tBlink = now;
    digitalWrite(LED_RUN, !digitalRead(LED_RUN));
  } else if (!running) {
    digitalWrite(LED_RUN, LOW);
  }
}
